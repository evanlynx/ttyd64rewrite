#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 1
    [MapTattle]  $Function_GetTattle
}

#new:EntryList $EntryList
{
	~Vec4f:Entry0
}

#new:Script_Main $Script_Main
{
	Set   *GB_WorldLocation  .Location:DryDryOutpost
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True )
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Call  MakeNpcs  ( .False $NpcGroupList_dro03 )
	Exec  $Script_BindScripts
    ExecWait  $Script_MakeEntities
	Exec  $Script_SetupMusic
    Set   *Var0 $Script_CreateExitTriggers
	Exec  EnterWalk
	Return
	End
}

#new:Script $Script_SetupMusic
{
	Call  SetMusicTrack ( 00000000 .Song:DryDryOutpost 00000000 00000008 )
	Call  ClearAmbientSounds ( 250` )
	Return
	End
}

#new:Script $Script_Exit_Entry0
{
	SetGroup 0000001B
	Call     UseExitHeading ( 60` ~Entry:Entry0 )
	Exec     ExitWalk
	Call  GotoMap   ( "dro_02" ~Entry:dro_02:Entry5 )
	Wait  100`
	Return
	End
}

#new:Script $Script_CreateExitTriggers
{
	Bind  $Script_Exit_Entry0 .Trigger:FloorAbove ~Collider:Exit0 00000001 00000000
	Return
	End
}

#new:FloatTable $FloatTable_Gondola
{
	~Vec3f:Pos_Gondola_Start ~Vec3f:Pos_Gondola_End
}

#new:Script $Script_BindScripts
{
	NewArray  00000007 *Var0
	UseArray  *Var0
	Set   *Array[0]  $FloatTable_Gondola
	Set   *Array[1]  ~Model:kassya_koma
	Set   *Array[2]  ~Model:kassya_himo
	Set   *Array[3]  ~PosXd:Pos_Gondola_PartnerStartPos
	Set   *Array[4]  ~PosYd:Pos_Gondola_PartnerStartPos
	Set   *Array[5]  ~PosZd:Pos_Gondola_PartnerStartPos
	%Set   *Array[6]  00000001 % npcID % unused (Used to move with an NPC too)
	Bind  $Script_Rope .Trigger:FloorPressA ~Collider:ZipStart 00000001 00000000
	Return
	End
}

#new:Script $Script_MakeEntities
{
    Call  MakeEntity    ( .Entity:Chest ~Vec4d:Entity_Chest 00000000 80000000 )
    Call  AssignFlag    ( *GF_DRO03_Chest )
    Call  AssignScript  ( $Script_OpenChest )
    Return
    End
}

#new:Script $Script_ShowGotItem
{
    0:  SetGroup  00000000
    C:  Call  SetTimeFreezeMode ( 00000002 )
   1C:  Wait  40`
   28:  Call  ShowGotItem       ( *Var0 .False 00000000 )
   40:  Call  SetTimeFreezeMode ( 00000000 )
   50:  Return
   58:  Return
   60:  End
}

#new:Script $Script_GiveItem
{
    0:  Call  DisablePlayerInput    ( .True )
   10:  Set   *Var0  *VarA
   20:  If  *VarA  !=  00000000
   30:  	ExecWait  $Script_ShowGotItem
   3C:  EndIf
   44:  Switch  *VarB
   50:  	Case  ==  00000000
   5C:  		Call  AddItem   ( *VarA *Var0 )
   70:  	Case  ==  00000001
   7C:  		Call  AddKeyItem    ( *VarA )
   8C:  	Case  ==  00000002
   98:  		Call  AddBadge  ( *VarA *Var0 )
   AC:  EndSwitch
   B4:  Wait  15`
   C0:  Call  DisablePlayerInput    ( .False )
   D0:  Return
   D8:  End
}

#new:Script $Script_OpenChest
{
    0:  Set   *VarA  .Item:DamageDodgeP
   10:  Set   *VarB  00000002 % Badge
   20:  Set   *GF_DRO03_Chest  .True
   30:  ExecWait  $Script_GiveItem
   3C:  Return
   44:  End
}

% Rope

#new:Script $Script_Rope % $Script_80244470 (kzn_09.mscr)
{
    0:  Call  GetCurrentPartner ( *Var9 )
   10:  If  *Var9  ==  .Partner:Kooper % 2
   20:  	Return
   28:  EndIf
   30:  If  *Var9  ==  .Partner:Bombette % 3
   40:  	Return
   48:  EndIf
   50:  UseArray  *Var0
   5C:  Call  DisablePlayerInput    ( .True )
   6C:  Call  DisablePlayerPhysics  ( .True )
   7C:  Call  SetPlayerActionState  ( .ActionState:Jump )
   8C:  Wait  1`
   98:  Exec  $Script_StickCameraToPlayerPos *Var9
   A8:  Thread
   B0:  	Wait  7`
   BC:  	Call  DisablePartnerAI  ( 00000000 )
   CC:  	Call  SetNpcFlagBits    ( .Npc:Partner 00000200 .False )
   E4:  	Call  SetNpcFlagBits    ( .Npc:Partner 00000140 .True )
   FC:  	Set   *Var0  *Array[4]
  10C:  	Sub   *Var0  0000000A
  11C:  	Set   *Var1  *Array[5]
  12C:  	Add   *Var1  FFFFFFFB
  13C:  	Call  SetNpcJumpscale   ( .Npc:Partner *Fixed[0.5] )
  150:  	Call  NpcJump0  ( .Npc:Partner *Array[3] *Var0 *Var1  8` )
  170:  	Call  SetNpcAnimation   ( .Npc:Partner 00000106 )
  184:  EndThread
  18C:  Call  SetPlayerJumpscale    ( *Fixed[0.2] )
        Call  GetModelCenter ( *Array[2] )
        Sub   *Var1 57` % y
        Add   *Var2 3 % z
        Call  PlayerJump    ( *Var0 *Var1 *Var2 5` )
  1B8:  Call  SetPlayerActionState  ( .ActionState:Land )
  1C8:  Wait  1`
  1D4:  Call  SetPlayerAnimation    ( 0008000D )
  1E4:  Wait  10`
  1F0:  Call  GetPlayerPos  ( *Var2 *Var3 *Var4 )
  208:  Call  PlaySound ( 80000019 )
  /%218:  ChildThread
  220:  	Set   *MapFlag[0A]  .True
  230:  	Set   *Var0  *Array[6]
  240:  	Set   *AreaByte[0]  *Array[6]
  250:  	Loop
  25C:  		Call  GetNpcPos ( *Var0 *Var1 *Var2 *Var3 )
  278:  		Call  $Function_80240D40    ( *MapFlag[0A] *Var1 *Var2 *Var3 )
  294:  		If  *AreaByte[0]  !=  *Var0
  2A4:  			BreakLoop
  2AC:  		EndIf
  2B4:  		Wait  1`
  2C0:  	EndLoop
  2C8:  EndChildThread%/
  2D0:  Call  MakeLerp  ( 00000000 000003E8 00000046 .Easing:QuadraticIn )
  2EC:  Label  0
  2F8:  Call  UpdateLerp ( )
  304:  Call  $Function_80240E88    ( 00000000 )
  314:  Call  TranslateModel    ( *Array[1] *Var5 *Var6 *Var7 )
  330:  Call  TranslateModel    ( *Array[2] *Var5 *Var6 *Var7 )
  34C:  Wait  1`
  /%358:  Call  $Function_TrackInputs_ButtonA ( )
  364:  If  *Var8  ==  00008000 % A button was pressed
  374:  	Goto  A
  380:  EndIf%/
  388:  If  *Var1  ==  00000001
  398:  	Goto  0
  3A4:  EndIf
  3AC:  Call  PlaySound ( 00002087 )
  3BC:  Label  A
  3C8:  Call  SetPlayerFlagBits ( 00800000 .True )
  3DC:  Set   *MapFlag[0A]  .False
  3EC:  Call  StopSound ( 80000019 )
  3FC:  Call  SetNpcFlagBits    ( .Npc:Partner 00000040 .False )
  414:  Call  EnablePartnerAI ( )
  420:  Call  DisablePlayerPhysics  ( .False )
  430:  Call  DisablePlayerInput    ( .False )
  440:  Kill  *Var9
  44C:  Wait  20`
  458:  Call  PlaySound ( 8000001A )
  /%468:  ChildThread
  470:  	Set   *MapFlag[0B]  .True
  480:  	Set   *Var0  *Array[6]
  490:  	Set   *AreaByte[1]  *Array[6]
  4A0:  	Loop
  4AC:  		Call  GetNpcPos ( *Var0 *Var1 *Var2 *Var3 )
  4C8:  		Call  $Function_80240DE4    ( *MapFlag[0B] *Var1 *Var2 *Var3 )
  4E4:  		If  *AreaByte[1]  !=  *Var0
  4F4:  			BreakLoop
  4FC:  		EndIf
  504:  		Wait  1`
  510:  	EndLoop
  518:  EndChildThread%/
  520:  Set   *Var2  *Var0
  530:  Div   *Var2  0000000A
  540:  Call  MakeLerp  ( *Var0 00000000 *Var2 .Easing:Linear )
  55C:  Label  1
  568:  Call  UpdateLerp ( )
  574:  Call  $Function_80240E88    ( 00000001 )
  584:  Call  TranslateModel    ( *Array[1] *Var5 *Var6 *Var7 )
  5A0:  Call  TranslateModel    ( *Array[2] *Var5 *Var6 *Var7 )
  5BC:  Wait  1`
  5C8:  If  *Var1  ==  00000001
  5D8:  	Goto  1
  5E4:  EndIf
  5EC:  Set   *MapFlag[0B]  .False
  5FC:  Call  StopSound ( 8000001A )
  60C:  Return
  614:  End
}

#new:Script $Script_StickCameraToPlayerPos
{
    0:  Label  0
    C:  Call  GetPlayerPos  ( *Var0 *Var1 *Var2 )
   24:  Call  SetCamTarget  ( .Cam:Default *Var0 *Var1 *Var2 )
   40:  Wait  1`
   4C:  Goto  0
   58:  Return
   60:  End
}

#new:Function $Function_80240D40
{
    0:  ADDIU     SP, SP, FFD8
    4:  SW        S1, 1C (SP)
    8:  COPY      S1, A0
    C:  SW        RA, 20 (SP)
   10:  SW        S0, 18 (SP)
   14:  LW        S0, C (S1)
   18:  LW        V0, 0 (S0)
   1C:  BEQ       V0, R0, .o8C
   20:  ADDIU     S0, S0, 4
   24:  LW        A1, 0 (S0)
   28:  JAL       ~Func:get_variable
   2C:  ADDIU     S0, S0, 4
   30:  SW        V0, 70 (S1)
   34:  LW        A1, 0 (S0)
   38:  ADDIU     S0, S0, 4
   3C:  JAL       ~Func:get_variable
   40:  COPY      A0, S1
   44:  SW        V0, 74 (S1)
   48:  LW        A1, 0 (S0)
   4C:  JAL       ~Func:get_variable
   50:  COPY      A0, S1
   54:  LI        A0, 168
   58:  CLEAR     A1
   5C:  LWC1      F0, 70 (S1)
   60:  CVT.S.W   F0, F0
   64:  LWC1      F2, 74 (S1)
   68:  CVT.S.W   F2, F2
   6C:  MFC1      A2, F0
   70:  MFC1      A3, F2
   74:  MTC1      V0, F0
   78:  NOP
   7C:  CVT.S.W   F0, F0
   80:  SW        V0, 78 (S1)
   84:  JAL       ~Func:sfx_adjust_env_sound_pos
   88:  SWC1      F0, 10 (SP)
        .o8C
   8C:  LW        RA, 20 (SP)
   90:  LW        S1, 1C (SP)
   94:  LW        S0, 18 (SP)
   98:  LI        V0, 2
   9C:  JR        RA
   A0:  ADDIU     SP, SP, 28
}

#new:Function $Function_80240E88
{
    0:  ADDIU     SP, SP, FFE0
    4:  SW        S1, 14 (SP)
    8:  COPY      S1, A0
    C:  SW        RA, 1C (SP)
   10:  SW        S2, 18 (SP)
   14:  SW        S0, 10 (SP)
   18:  LW        V0, C (S1)
   1C:  JAL       ~Func:get_variable
   20:  LW        A1, 0 (V0)
   24:  LW        V1, 13C (S1)
   28:  LW        S2, 0 (V1)
   2C:  LWC1      F6, C (S2)
   30:  LWC1      F0, 0 (S2)
   34:  SUB.S     F6, F6, F0
   38:  LIF       F8, 1000.0
   40:  LWC1      F0, 84 (S1)
   44:  CVT.S.W   F0, F0
   48:  DIV.S     F6, F6, F8
   4C:  MUL.S     F6, F6, F0
   50:  NOP
   54:  LWC1      F4, 10 (S2)
   58:  LWC1      F2, 4 (S2)
   5C:  SUB.S     F4, F4, F2
   60:  LWC1      F2, 8 (S2)
   64:  LWC1      F0, 14 (S2)
   68:  SUB.S     F0, F0, F2
   6C:  DIV.S     F4, F4, F8
   70:  LWC1      F2, 84 (S1)
   74:  CVT.S.W   F2, F2
   78:  MUL.S     F4, F4, F2
   7C:  NOP
   80:  DIV.S     F0, F0, F8
   84:  MUL.S     F0, F0, F2
   88:  NOP
   8C:  TRUNC.W.S F10, F6
   90:  SWC1      F10, 98 (S1)
   94:  TRUNC.W.S F10, F4
   98:  SWC1      F10, 9C (S1)
   9C:  TRUNC.W.S F10, F0
   A0:  BNE       V0, R0, .o15C
   A4:  SWC1      F10, A0 (S1)
   A8:  JAL       ~Func:get_npc_safe
   AC:  LI        A0, FFFC
   B0:  LW        V1, 8C (S1)
   B4:  LW        A0, 98 (S1)
   B8:  LA        S0, 8010EFC8
   C0:  ADDU      V1, V1, A0
   C4:  MTC1      V1, F0
   C8:  NOP
   CC:  CVT.S.W   F0, F0
   D0:  SWC1      F0, 28 (S0)
   D4:  LW        V1, 90 (S1)
   D8:  LW        A0, 9C (S1)
   DC:  ADDU      V1, V1, A0
   E0:  MTC1      V1, F0
   E4:  NOP
   E8:  CVT.S.W   F0, F0
   EC:  SWC1      F0, 2C (S0)
   F0:  LW        V1, 94 (S1)
   F4:  LW        A0, A0 (S1)
   F8:  ADDU      V1, V1, A0
   FC:  MTC1      V1, F0
  100:  NOP
  104:  CVT.S.W   F0, F0
  108:  SWC1      F0, 30 (S0)
  10C:  LWC1      F12, 0 (S2)
  110:  LWC1      F14, 8 (S2)
  114:  LW        A2, C (S2)
  118:  LW        A3, 14 (S2)
  11C:  JAL       ~Func:atan2
  120:  COPY      S1, V0
  124:  LWC1      F2, 28 (S0)
  128:  SWC1      F0, 80 (S0)
  12C:  SWC1      F2, 38 (S1)
  130:  LWC1      F0, 2C (S0)
  134:  LIF       F2, 10.0
  13C:  NOP
  140:  SUB.S     F0, F0, F2
  144:  LIF       F2, 5.0
  14C:  SWC1      F0, 3C (S1)
  150:  LWC1      F0, 30 (S0)
  154:  SUB.S     F0, F0, F2
  158:  SWC1      F0, 40 (S1)
        .o15C
  15C:  LW        RA, 1C (SP)
  160:  LW        S2, 18 (SP)
  164:  LW        S1, 14 (SP)
  168:  LW        S0, 10 (SP)
  16C:  LI        V0, 2
  170:  JR        RA
  174:  ADDIU     SP, SP, 20
}

#new:Function $Function_TrackInputs_ButtonA
{
    0:  LAW       V0, 8007419C
    8:  LW        V0, 10 (V0)
    C:  ANDI      V0, V0, 8000
   10:  SW        V0, A4 (A0) % save in var8
   14:  JR        RA
   18:  LI        V0, 2
}

#new:Function $Function_80240DE4
{
    0:  ADDIU     SP, SP, FFD8
    4:  SW        S1, 1C (SP)
    8:  COPY      S1, A0
    C:  SW        RA, 20 (SP)
   10:  SW        S0, 18 (SP)
   14:  LW        S0, C (S1)
   18:  LW        V0, 0 (S0)
   1C:  BEQ       V0, R0, .o8C
   20:  ADDIU     S0, S0, 4
   24:  LW        A1, 0 (S0)
   28:  JAL       ~Func:get_variable
   2C:  ADDIU     S0, S0, 4
   30:  SW        V0, 70 (S1)
   34:  LW        A1, 0 (S0)
   38:  ADDIU     S0, S0, 4
   3C:  JAL       ~Func:get_variable
   40:  COPY      A0, S1
   44:  SW        V0, 74 (S1)
   48:  LW        A1, 0 (S0)
   4C:  JAL       ~Func:get_variable
   50:  COPY      A0, S1
   54:  LI        A0, 185
   58:  CLEAR     A1
   5C:  LWC1      F0, 70 (S1)
   60:  CVT.S.W   F0, F0
   64:  LWC1      F2, 74 (S1)
   68:  CVT.S.W   F2, F2
   6C:  MFC1      A2, F0
   70:  MFC1      A3, F2
   74:  MTC1      V0, F0
   78:  NOP
   7C:  CVT.S.W   F0, F0
   80:  SW        V0, 78 (S1)
   84:  JAL       ~Func:sfx_adjust_env_sound_pos
   88:  SWC1      F0, 10 (SP)
        .o8C
   8C:  LW        RA, 20 (SP)
   90:  LW        S1, 1C (SP)
   94:  LW        S0, 18 (SP)
   98:  LI        V0, 2
   9C:  JR        RA
   A0:  ADDIU     SP, SP, 28
}

% NPC data

#new:NpcGroupList $NpcGroupList_dro03
{
	00000005 $NpcGroup_Town 00000000
	00000000 00000000 00000000
}

#new:NpcGroup $NpcGroup_Town
{
	00000000 $NpcSettings_Generic ~Vec3f:NPC_Toad1
	00112D09 $Script_Init_NPC_Toad1 00000000 00000000 00000000
	~NoDrops
	~Movement:NPC_Toad1
	~AnimationTable:NPC_Toad1
	00000000 00000000 00000000 $Tattle_Dryite
	%
	% $NpcGroup_Town[1F0]
	00000001 $NpcSettings_Generic ~Vec3f:NPC_Toad2
	00112D09 $Script_Init_NPC_Toad2 00000000 00000000 0000010E
	~NoDrops
	~Movement:NPC_Toad2
	~AnimationTable:NPC_Toad2
	00000000 00000000 00000000 $Tattle_Dryite
	%
	% $NpcGroup_Town[3E0]
	00000002 $NpcSettings_Generic ~Vec3f:NPC_Toad3
	00112D09 $Script_Init_NPC_Toad3 00000000 00000000 00000000
	~NoDrops
	~Movement:NPC_Toad3
	~AnimationTable:NPC_Toad3
	00000000 00000000 00000000 $Tattle_Dryite
	%
	% $NpcGroup_Town[5D0]
	00000003 $NpcSettings_Generic ~Vec3f:NPC_Mouser1
	00112D09 $Script_Init_NPC_Mouser1 00000000 00000000 0000010E
	~NoDrops
	~Movement:NPC_Mouser1
	~AnimationTable:NPC_Mouser1
	00000000 00000000 00000000 $Tattle_Dryite
	%
	% $NpcGroup_Town[7C0]
	00000004 $NpcSettings_Generic ~Vec3f:NPC_Goomshu
	00112D09 $Script_Init_NPC_Goomshu 00000000 00000000 0000010E
	~NoDrops
	~Movement:NPC_Goomshu
	~AnimationTable:NPC_Goomshu
	00000000 00000000 00000000 $Tattle_Goomshu
}

#new:NpcSettings $NpcSettings_Generic
{
	00000000 001E0018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:Script $Script_Init_NPC_Toad1
{
    Call  BindNpcInteract   ( .Npc:Self $Script_Interact_Toad1 )
    Return
    End
}

#new:Script $Script_Interact_Toad1
{
    Call    SpeakToPlayer ( .Npc:Self 00930304 00930301 00000000 $String_Toad1_1 )
    Return
    End
}

%

#new:Script $Script_Init_NPC_Toad2
{
    Call  BindNpcInteract   ( .Npc:Self $Script_Interact_Toad2 )
    Return
    End
}

#new:Script $Script_Interact_Toad2
{
    Call    SpeakToPlayer ( .Npc:Self 00930204 00930201 00000000 $String_Toad2_1 )
    Return
    End
}

%

#new:Script $Script_Init_NPC_Toad3
{
    Call  BindNpcInteract   ( .Npc:Self $Script_Interact_Toad3 )
    Return
    End
}

#new:Script $Script_Interact_Toad3
{
    Call    SpeakToPlayer ( .Npc:Self 00930304 00930301 00000000 $String_Toad3_1 )
    Return
    End
}

%

#new:Script $Script_Init_NPC_Mouser1
{
    Call  BindNpcInteract   ( .Npc:Self $Script_Interact_Mouser1 )
    Return
    End
}

#new:Script $Script_Interact_Mouser1
{
	Call	ShowCoinCounter ( .True )
	% check if player can buy this item
	Call	AddCoin ( 0 )
	If *Var0 < 10` % Not enough money
		Call    SpeakToPlayer ( .Npc:Self 00940006 00940001 00000000 $String_Mouser1_2 )
		Call    SpeakToPlayer ( .Npc:Self 00940006 00940001 00000000 $String_Mouser_Error_Money )
		Call	ShowCoinCounter ( .False )
		Return
	EndIf
	Call	$Function_GetFreeItemSlots
	If  *Var0  ==  .False % Not free invetory space
		Call    SpeakToPlayer ( .Npc:Self 00940006 00940001 00000000 $String_Mouser1_2 )
		Call	ShowCoinCounter ( .False )
		Call    SpeakToPlayer ( .Npc:Self 00940006 00940001 00000000 $String_Mouser_Error_Space )
		Return
	EndIf
    Call    SpeakToPlayer ( .Npc:Self 00940006 00940001 00000000 $String_Mouser1_1 )
	Call  	ShowChoice    ( 001E000D ) % Yes No
	If *Var0 == 0 % Yes
		Call	AddCoin ( -10` )
		Call	ShowCoinCounter ( .False )
		Call    ContinueSpeech ( .Npc:Self 00940006 00940001 00000000 $String_Mouser_Thanks )
		Call	ShowGotItem ( .Item:HotSauce .True 00000000 )
		Call	AddItem ( .Item:HotSauce 0 )
	Else
		Call	ShowCoinCounter ( .False )
		Call    ContinueSpeech ( .Npc:Self 00940006 00940001 00000000 $String_Mouser_Cancel )
	EndIf
    Return
    End
}

%

#new:Script $Script_Init_NPC_Goomshu
{
    Call  BindNpcInteract   ( .Npc:Self $Script_Interact_Goomshu )
    Return
    End
}

#new:Script $Script_Interact_Goomshu
{
    Call	ShowCoinCounter ( .True )
	% check if player can buy this item
	Call	AddCoin ( 0 )
	If *Var0 < 10` % Not enough money
		Call    SpeakToPlayer ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Goomshu_2 )
		Call    SpeakToPlayer ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Goomshu_Error_Money )
		Call	ShowCoinCounter ( .False )
		Return
	EndIf
	Call	$Function_GetFreeItemSlots
	If  *Var0  ==  .False % Not free invetory space
		Call    SpeakToPlayer ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Goomshu_2 )
		Call	ShowCoinCounter ( .False )
		Call    SpeakToPlayer ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Goomshu_Error_Space )
		Return
	EndIf
    Call    SpeakToPlayer ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Goomshu_1 )
	Call  	ShowChoice    ( 001E000D ) % Yes No
	If *Var0 == 0 % Yes
		Call	AddCoin ( -10` )
		Call	ShowCoinCounter ( .False )
		Call    ContinueSpeech ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Mouser_Thanks )
		Call	ShowGotItem ( .Item:HotDog .True 00000000 )
		Call	AddItem ( .Item:HotDog 0 )
	Else
		Call	ShowCoinCounter ( .False )
		Call    ContinueSpeech ( .Npc:Self 00ED0002 00ED0001 00000000 $String_Mouser_Cancel )
	EndIf
    Return
    End
}

% returns in var0
#new:Function $Function_GetFreeItemSlots
{
    0:  ADDIU     SP, SP, FFE8
    4:  SW        S0, 10 (SP)
    8:  SW        RA, 14 (SP)
    C:  JAL       ~Func:get_item_empty_count
   10:  DADDU     S0, A0, R0
   14:  SW        V0, 84 (S0)
   18:  LW        RA, 14 (SP)
   1C:  LW        S0, 10 (SP)
   20:  ADDIU     V0, R0, 2
   24:  JR        RA
   28:  ADDIU     SP, SP, 18
}

% Strings

#string $String_Toad1_1
{
[Style Right]
I once heard a rumor about a[BR]
secret place deep underground,[BR]
full of dangerous monsters![BR]
[Wait][Next]
It sounds horrible![BR]
[Wait][Next]
But it makes you wonder, what[BR]
sorts of treasure could be[BR]
hidden at the bottom?
[Wait][End]
}

#string $String_Toad2_1
{
[Style Right]
Wait, I got a better one.[BR]
Have you heard about the newest[BR]
video game console?[BR]
[Wait][Next]
My friend who lives up near[BR]
Star Summit got one early![BR]
He says his uncle works at the[BR]
company that makes them![BR]
[Wait][Next]
We should go play sometime!
[Wait][End]
}

#string $String_Toad3_1
{
[Style Right]
Ah, I love that warm sun![BR]
Hot days really are the best.[BR]
[Wait][Next]
I love the heat, I really don't[BR]
understand why a lot of people[BR]
prefer the cold to be honest.
[Wait][End]
}

%

#string $String_Mouser1_1
{
[Style Right]
Hey, hey! Welcome![BR]
[Wait][Next]
I sell the hottest Hot Sauce,[BR]
for only [SaveColor][Color:07]10 coins[RestoreColor].[BR]
Would you be interested?[BR]
[Yield][End]
}

#string $String_Mouser1_2
{
[Style Right]
Welcome, welcome![BR]
[Wait][Next]
I sell the hottest Hot Sauce,[BR]
for only [SaveColor][Color:07]10 coins[RestoreColor].[BR]
[Wait][End]
}

#string $String_Mouser_Error_Money
{
[Style Right]
That won't be enough coins,[BR]
unfortunately. No discounts![BR]
[Wait][End]
}

#string $String_Mouser_Error_Space
{
[Style Right]
Unfortunately, you have nowhere[BR]
to put it! Clear out some space[BR]
and come back later!
[Wait][End]
}

#string $String_Goomshu_1
{
[Style Right]
Welcome![BR]
I have delicious hot dogs![BR]
[Wait][Next]
You want one?[BR]
Only [SaveColor][Color:07]10 coins[RestoreColor] each![BR]
[Yield][End]
}

#string $String_Goomshu_2
{
[Style Right]
Welcome![BR]
Hot dogs for sale![BR]
[Wait][Next]
You want it? It's yours![BR]
Only [SaveColor][Color:07]10 coins[RestoreColor]![BR]
[Wait][End]
}

#string $String_Goomshu_Error_Money
{
[Style Right]
Sorry Mario, I can't give credit,[BR]
come back when you're a little[BR]
mmmmm... richer!
[Wait][End]
}

#string $String_Goomshu_Error_Space
{
[Style Right]
Sorry, but you don't have[BR]
enough space in your inventory!
[Wait][End]
}

#string $String_Mouser_Cancel
{
[Next]
That's too bad![BR]
Maybe some other time.
[Wait][End]
}

#string $String_Mouser_Thanks
{
[Next]
Thanks a lot![BR]
Come back soon.
[Wait][End]
}

%

#string $Tattle_Dryite
{
[Style Tattle]
One of the locals here at[BR]
Dry Dry Outpost. Hard to believe[BR]
anyone's made a life for themselves[BR]
in this boiling place![BR]
[Wait][Next]
They seem to be making the most[BR]
of it, though.
[Wait][End]
}

#string $Tattle_Goomshu
{
[Style Tattle]
That's Goomshu. He's a well-known[BR]
merchant here in Dry Dry Outpost.[BR]
[Wait][Next]
I heard he comes from a far away[BR]
land, where he used to run a[BR]
thriving business selling lamp oil,[BR]
ropes, bombs, and other stuff.[BR]
[Wait][Next]
I wonder what caused him to pick up[BR]
and move all the way out here?
[Wait][End]
}

% Tattle

#new:Function $Function_GetTattle
{
LIO       V0, $String_Tattle
JR        RA
RESERVED
}

#string $String_Tattle
{
[Style Tattle][EnableCDownNext]
This is a bazaar, where all sorts of[BR]
merchants get together to peddle[BR]
their goods.[BR]
[Wait][Next]
That guy selling hot dogs seems[BR]
out of place, don't you think?
[Wait][End]
}
