#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 2
    [MapTattle]  $Function_GetTattle
}

#new:EntryList $EntryList
{
	~Vec4f:Entry0
    ~Vec4f:Entry1
}

#new:Script_Main $Script_Main
{
	Set   *GB_WorldLocation  .Location:ShiverMountain
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True )
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
	Exec  $Script_SetupMusic
    Exec  $Script_InitStuff
    Exec  $Script_EnterMap
	Return
	End
}

#new:Script $Script_InitStuff
{
    Set   *GB_SAM07_Floor 2
    Call  MakeItemEntity ( .Item:TornadoJump_1 ~Vec3d:itemPos1 .ItemSpawnMode:Fixed_NeverVanish *GF_SAM13_Item1 )
    Bind  $Script_SetRespawnPos_1 .Trigger:FloorTouch ~Collider:respawn1 00000001 00000000
    Bind  $Script_SetRespawnPos_2 .Trigger:FloorTouch ~Collider:respawn2 00000001 00000000
    Bind  $Script_SetRespawnPos_3 .Trigger:FloorTouch ~Collider:respawn3 00000001 00000000
    Bind  $Script_SetRespawnPos_4 .Trigger:FloorTouch ~Collider:respawn4 00000001 00000000
    Bind  $Script_SetRespawnPos_5 .Trigger:FloorTouch ~Collider:respawn5 00000001 00000000
    Bind  $Script_SetRespawnPos_6 .Trigger:FloorTouch ~Collider:respawn6 00000001 00000000
    %Bind  $Script_Respawn .Trigger:FloorAbove ~Collider:respawnTrigger 00000001 00000000
    Exec  $Script_TrackDeathTrigger
    Return
    End
}

#new:Script $Script_TrackDeathTrigger
{
    Loop
        Call    GetPlayerPos ( *Var0 *Var1 *Var2 )
        If *Var1 < ~PosYd:respawnTrigger
            ExecWait $Script_Respawn
        EndIf
        Wait    1
    EndLoop
    Return
    End
}

#new:Script $Script_Respawn
{
    Call    DisablePlayerInput ( .True )
    SetF	*Var0 *Fixed[25.0] % speed
    SetF	*Var1 *Fixed[255.0]  % dark
    ExecWait $Script_FadeScreenOut
    Switch *MapVar[0]
        Case == 0
            Call    SetPlayerPos ( ~Vec3d:respawn1 )
        Case == 1
            Call    SetPlayerPos ( ~Vec3d:respawn2 )
        Case == 2
            Call    SetPlayerPos ( ~Vec3d:respawn3 )
        Case == 3
            Call    SetPlayerPos ( ~Vec3d:respawn4 )
        Case == 4
            Call    SetPlayerPos ( ~Vec3d:respawn5 )
        Case == 5
            Call    SetPlayerPos ( ~Vec3d:respawn6 )
    EndSwitch
    Call    GetPlayerPos ( *Var0 *Var1 *Var2 )
    Call    UseSettingsFrom ( .Cam:Default *Var0 *Var1 *Var2 )
    Call    ResetCam ( .Cam:Default *Fixed[200.0] )
    Call    TeleportPartnerToPlayer
    Call    SetPlayerActionState ( .ActionState:Idle )
    Wait    1
    Call    SetPlayerAnimation ( 00010002 )
    Call    SetPlayerAnimation ( 0001000F )
    SetF	*Var0 *Fixed[25.0] % speed
    SetF	*Var1 *Fixed[5.0]  % fully bright
    Exec    $Script_FadeScreenIn
    Wait    10`
    Call    SetPlayerAnimation ( 00010030 )
    Wait    15`
    Call    SetPlayerAnimation ( 00010031 )
    Thread
        Call    PlaySound ( 0172 )
        Wait    12`
        Call    PlaySound ( 0172 )
    EndThread
    Wait    25`
    Call    SetPlayerAnimation ( 00010002 )
    Call    DisablePlayerInput ( .False )
    Return
    End
}

#new:Script $Script_SetRespawnPos_1
{
    Set *MapVar[0] 0
    Return
    End
}

#new:Script $Script_SetRespawnPos_2
{
    Set *MapVar[0] 1
    Return
    End
}

#new:Script $Script_SetRespawnPos_3
{
    Set *MapVar[0] 2
    Return
    End
}

#new:Script $Script_SetRespawnPos_4
{
    Set *MapVar[0] 3
    Return
    End
}

#new:Script $Script_SetRespawnPos_5
{
    Set *MapVar[0] 4
    Return
    End
}

#new:Script $Script_SetRespawnPos_6
{
    Set *MapVar[0] 5
    Return
    End
}

#new:Script $Script_SetupMusic
{
	Call  SetMusicTrack ( 00000000 .Song:ShiverMountain 00000000 00000008 )
	Call  ClearAmbientSounds ( 250` )
	Return
	End
}

#new:Script $Script_EnterMap
{
    Call  GetEntryID    ( *Var[0] )
	Switch  *Var[0]
		Case  ==  ~Entry:Entry0
			Set   *Var0  $Script_CreateExitTriggers
    		Exec  EnterWalk
		Case  ==  ~Entry:Entry1
			Set   *Var[A] $Script_CreateExitTriggers
			Exec  $Script_EnterVerticalPipe
	EndSwitch
    Wait  1
	Return
	End
}

#new:Script $Script_CreateExitTriggers
{
    Bind  $Script_Exit0 .Trigger:FloorTouch ~Collider:Exit0 00000001 00000000
    Bind  $Script_Exit1 .Trigger:FloorTouch ~Collider:Exit1 00000001 00000000
    Return
    End
}

#new:Script $Script_Exit0 {
    Call  UseExitHeading  	( 3C ~Entry:Entry0 )
	Exec  ExitWalk
    Call  GotoMap   ( "sam_07" 00000002 )
    Wait  100`
    Return
    End
}

#new:Script $Script_Exit1
{
	SetGroup 0000001B
	Set   *Var[A] ~Entry:Entry1
	Set   *Var[B] ~Collider:Exit1
	Set   *Var[C] $Script_GotoMap_Exit1
	ExecWait  $Script_ExitVerticalPipe
	Return
	End
}

#new:Script $Script_GotoMap_Exit1
{
	Call  GotoMap   ( "tyd_07" 00000000 )
	Wait  100`
	Return
	End
}

% pipes code

#new:Function $Function_Pipe_SetAnimFlag
{
	LAW       V0, 800F7B30
	LW        V1, 4 (V0)
	LUI       A0, 10
	OR        V1, V1, A0
	SW        V1, 4 (V0)
	JR        RA
	ADDIU     V0, R0, 2
}

#new:Function $Function_Pipe_GetCurrentFloor
{
	LAH       V0, 8015A552
	SW        V0, 84 (A0)
	JR        RA
	ADDIU     V0, R0, 2
}

#new:Function $Function_Pipe_CheckDownInput
{
	LAH       V1, 8015A552
	ADDIU     SP, SP, FFE8
	SW        S0, 10 (SP)
	COPY      S0, A0
	SW        RA, 14 (SP)
	LW        V0, B0 (S0)
	BEQ       V1, V0, .o2C
	ADDIU     V0, R0, 2
	BEQ       R0, R0, .oAC
	SW        R0, 84 (S0)
	.o2C
	LAW       V1, 8007419C
	LB        V0, 40 (V1)
	LB        A3, 44 (V1)
	BLTZL     V0, .o44
	SUBU      V0, R0, V0
	.o44
	BNE       V0, R0, .o54
	NOP
	BEQL      A3, R0, .oAC
	CLEAR     V0
	.o54
	MTC1      V0, F4
	NOP
	CVT.S.W   F4, F4
	MFC1      A2, F4
	MTC1      A3, F4
	NOP
	CVT.S.W   F4, F4
	MTC1      R0, F12
	MFC1      A3, F4
	JAL       ~Func:atan2
	MOV.S     F14, F12
	LIF       F2, 60.0
	NOP
	C.LT.S    F0, F2
	NOP
	BC1F      .oA8
	ADDIU     V0, R0, 1
	SW        V0, 84 (S0)
	BEQ       R0, R0, .oAC
	ADDIU     V0, R0, 2
	.oA8
	CLEAR     V0
	.oAC
	LW        RA, 14 (SP)
	LW        S0, 10 (SP)
	JR        RA
	ADDIU     SP, SP, 18
}

#new:Function $Function_Pipe_GetEntryCoords
{
	ADDIU     SP, SP, FFE0
	SW        S1, 14 (SP)
	COPY      S1, A0
	SW        RA, 1C (SP)
	SW        S2, 18 (SP)
	JAL       ~Func:get_current_map_header
	SW        S0, 10 (SP)
	COPY      A0, S1
	LIO       A1, *Var[0]
	JAL       ~Func:get_variable
	COPY      S2, V0
	COPY      A0, S1
	LUI       A1, FE36
	LW        V1, 14 (S2)
	SLL       S0, V0, 4
	ADDU      V1, S0, V1
	LWC1      F0, 0 (V1)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C81 % Var[1]
	COPY      A0, S1
	LW        V0, 14 (S2)
	LUI       A1, FE36
	ADDU      V0, S0, V0
	LWC1      F0, 4 (V0)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C82 % Var[2]
	COPY      A0, S1
	LW        V0, 14 (S2)
	LUI       A1, FE36
	ADDU      V0, S0, V0
	LWC1      F0, 8 (V0)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C83 % Var[3]
	COPY      A0, S1
	LW        V0, 14 (S2)
	LUI       A1, FE36
	ADDU      S0, S0, V0
	LWC1      F0, C (S0)
	TRUNC.W.S F2, F0
	MFC1      A2, F2
	JAL       ~Func:set_variable
	ORI       A1, A1, 3C84 % Var[4]
	LW        RA, 1C (SP)
	LW        S2, 18 (SP)
	LW        S1, 14 (SP)
	LW        S0, 10 (SP)
	ADDIU     V0, R0, 2
	JR        RA
	ADDIU     SP, SP, 20
}

#new:Function $Function_Pipe_GetCameraYaw
{
	LAW       V1, 80077410
	LIF       F12, 180.0
	ADDIU     SP, SP, FFE8
	SW        RA, 14 (SP)
	SW        S0, 10 (SP)
	SLL       V0, V1, 2
	ADDU      V0, V0, V1
	SLL       V0, V0, 2
	SUBU      V0, V0, V1
	SLL       V1, V0, 3
	ADDU      V0, V0, V1
	SLL       V0, V0, 3
	LTF       F0, V0 (800B1DEC)
	ADD.S     F12, F0, F12
	JAL       ~Func:clamp_angle
	COPY      S0, A0
	TRUNC.W.S F2, F0
	SWC1      F2, 84 (S0)
	LW        RA, 14 (SP)
	LW        S0, 10 (SP)
	ADDIU     V0, R0, 2
	JR        RA
	ADDIU     SP, SP, 18
}

#new:Function $Function_Pipe_GetPointInFrontOfPlayer
{
	ADDIU     SP, SP, FFD8
	SW        S0, 18 (SP)
	COPY      S0, A0
	SW        RA, 1C (SP)
	SDC1      F20, 20 (SP)
	LW        V0, C (S0)
	JAL       ~Func:get_float_variable
	LW        A1, 0 (V0)
	ADDIU     A0, SP, 10
	LIA       V0, 8010EFC8
	MFC1      A2, F0
	LWC1      F0, 28 (V0) % player X
	LWC1      F2, 30 (V0) % player Z
	SWC1      F0, 10 (SP) % arg0 for add_vec2D_polar
	SWC1      F2, 14 (SP) % arg1 for add_vec2D_polar
	LW        A3, 80 (V0)
	LWC1      F20, 2C (V0)
	JAL       ~Func:add_vec2D_polar
	ADDIU     A1, SP, 14
	COPY      A0, S0
	LUI       A1, FE36
	LW        A2, 10 (SP)
	JAL       ~Func:set_float_variable
	ORI       A1, A1, 3C80 % Var[0]
	COPY      A0, S0
	LUI       A1, FE36
	MFC1      A2, F20
	JAL       ~Func:set_float_variable
	ORI       A1, A1, 3C81 % Var[1]
	COPY      A0, S0
	LUI       A1, FE36
	LW        A2, 14 (SP)
	JAL       ~Func:set_float_variable
	ORI       A1, A1, 3C82 % Var[2]
	LW        RA, 1C (SP)
	LW        S0, 18 (SP)
	LDC1      F20, 20 (SP)
	ADDIU     V0, R0, 2
	JR        RA
	ADDIU     SP, SP, 28
}

#new:Script $Script_EnterVerticalPipe
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  DisablePartnerAI      ( 00000000 )
	Call  HidePlayerShadow      ( .True )
	Call  SetPlayerAnimation    ( .PlayerAnim:StandStill )
	Call  GetCurrentPartnerID   ( *Var[0] )
	If  *Var[0]  !=  .Partner:None % 0
		Call  EnableNpcShadow   ( .Npc:Partner .False )
		Call  SetNpcPos ( .Npc:Partner 00000000 -1000` 00000000 )
	EndIf
	Call  GetEntryID    ( *Var[0] )
	Call  $Function_Pipe_GetEntryCoords ( )
	Sub   *Var[2]  40`
	Call  SetPlayerPos  ( *Var[1] *Var[2] *Var[3] )
	Call  InterpPlayerYaw   ( *Var[4] 00000000 )
	Call  PlaySound     ( 00000163 )
	Call  802D286C      ( 00000100 )
	Call  802D2520      ( 00010000 00000005 00000002 00000001 00000001 00000000 )
	Call  GetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
	Loop  40`
		Add   *Var[1]  00000001
		Call  SetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
		Wait  1`
	EndLoop
	Wait  3`
	Call  GetCurrentPartnerID   ( *Var[0] )
	If  *Var[0]  !=  .Partner:None % 0
		Thread
			Call  DisablePartnerAI  ( 00000000 )
			Call  GetPlayerPos      ( *Var[0] *Var[1] *Var[2] )
			Sub   *Var[2]  00000003
			Call  SetNpcPos ( .Npc:Partner *Var[0] *Var[1] *Var[2] )
			Call  SetNpcFlagBits    ( .Npc:Partner 00000002 .False )
			Call  EnablePartnerAI ( )
			Call  EnableNpcShadow   ( .Npc:Partner .True )
		EndThread
	EndIf
	Wait  2`
	Call  802D2520  ( 00010000 00000000 00000000 00000000 00000000 00000000 )
	Wait  1`
	Call  SetPlayerAnimation    ( 00010002 )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Call  HidePlayerShadow      ( .False )
	Label  0
	Call  $Function_Pipe_GetCurrentFloor ( )
	Wait  1`
	If  *Var[0]  !=  FFFFFFFF % FFFFFFFF
		Goto  0
	EndIf
	Exec  *Var[A]
	Return
	End
}

#new:Script $Script_EnterHorizontalPipe
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  HidePlayerShadow      ( .True )
	Call  ModifyColliderFlags   ( 00000000 *Var[B] 7FFFFE00 )
	Call  GetEntryID    ( *Var[0] )
	Call  $Function_Pipe_GetEntryCoords ( )
	Set   *Var[5]  *Var[1]
	Set   *Var[6]  *Var[2]
	Set   *Var[7]  *Var[3]
	Add   *Var[2]  2`
	Call  SetPlayerPos  ( *Var[1] *Var[2] *Var[3] )
	Call  InterpPlayerYaw       ( *Var[4] 00000000 )
	If  *Var[4]  ==  90`
		Add   *Var[5]  40`
	Else
		Sub   *Var[5]  40`
	EndIf
	Call  UseSettingsFrom   ( .Cam:Default *Var[5] *Var[6] *Var[7] )
	Call  SetPanTarget      ( .Cam:Default *Var[5] *Var[6] *Var[7] )
	Call  SetCamSpeed       ( .Cam:Default *Fixed[90.0] )
	Call  PanToTarget       ( .Cam:Default 00000000 00000001 )
	Call  GetCurrentPartnerID   ( *Var[0] )
	If  *Var[0]  !=  .Partner:None % 0
		Call  DisablePartnerAI  ( 00000000 )
		Call  EnableNpcShadow   ( .Npc:Partner .False )
		Call  SetNpcPos ( .Npc:Partner 00000000 -1000` 00000000 )
		Call  InterpNpcYaw      ( .Npc:Partner *Var[0] 00000000 )
	EndIf
	Wait  1`
	Call  PlaySound ( 00000163 )
	Thread
		Wait  25`
		Call  HidePlayerShadow  ( .False )
	EndThread
	Call  802D286C  ( 00000900 )
	Call  802D2520  ( 00010002 00000005 00000003 00000001 00000001 00000000 )
	Loop  40`
		Call  $Function_Pipe_GetPointInFrontOfPlayer ( *Fixed[1.0] )
		Call  SetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
		Wait  1`
	EndLoop
	Call  GetCurrentPartnerID   ( *Var[0] )
	If  *Var[0]  !=  .Partner:None % 0
		Thread
			Call  GetPlayerPos      ( *Var[0] *Var[1] *Var[2] )
			Sub   *Var[2]  00000003
			Call  SetNpcPos ( .Npc:Partner *Var[0] *Var[1] *Var[2] )
			Call  EnableNpcShadow   ( .Npc:Partner .True )
			Call  EnablePartnerAI ( )
		EndThread
	EndIf
	Wait  5`
	Call  802D2520  ( 00010002 00000000 00000000 00000000 00000000 00000000 )
	Call  ModifyColliderFlags   ( 00000001 *Var[B] 7FFFFE00 )
	Call  DisablePlayerInput    ( .False )
	Call  DisablePlayerPhysics  ( .False )
	Call  PanToTarget   ( .Cam:Default 00000000 00000000 )
	Exec  *Var[A]
	Return
	End
}

/%
	ARGS:
	*Var[A]  entryID
	*Var[B]  colliderID
	*Var[C]  $Script_GotoMap
%/
#new:Script $Script_ExitVerticalPipe
{
	Call  $Function_Pipe_CheckDownInput ( )
	If  *Var[0]  ==  00000000
		Return
	EndIf
	Call  GetCurrentPartner ( *Var[0] )
	If  *Var[0]  !=  00000000
		Call  GetCurrentPartnerID   ( *Var[1] )
		If  *Var[1]  !=  .Partner:Watt % 6
			Return
		Else
			Call  802D2B6C ( )
			Call  DisablePlayerInput    ( .True )
		EndIf
	Else
		Call  DisablePlayerInput    ( .True )
	EndIf
/%
	ExecWait  $Script_80241230
	Return
	End
}

#new:Script $Script_80241230
{
%/
	SetGroup  0000001B
	Call  $Function_Pipe_SetAnimFlag ( )
	Call  DisablePlayerPhysics  ( .True )
	Call  HidePlayerShadow  ( .True )
	Set   *Var[0]  *Var[A]
	Call  $Function_Pipe_GetEntryCoords ( )
	Call  PlayerMoveTo      ( *Var[1] *Var[3] 00000003 )
	Set   *Var[0]  *Var[A]
	Call  $Function_Pipe_GetEntryCoords ( )
	Call  SetPlayerPos      ( *Var[1] *Var[2] *Var[3] )
	Call  SetPlayerFlagBits ( 00200000 .True )
	Call  $Function_Pipe_GetCameraYaw ( )
	Call  InterpPlayerYaw   ( *Var[0] 00000000 )
	Wait  2`
	Call  SetPlayerFlagBits ( 00200000 .False )
	Call  PlaySound ( 00000163 )
	Call  GetPlayerPos      ( *Var[0] *Var[1] *Var[2] )
	Thread
		Wait  4`
		Loop  40`
			Sub   *Var[1]  00000001
			Call  SetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
			Wait  1`
		EndLoop
	EndThread
	Call  802D286C  ( 00000800 )
	Call  802D2520  ( 00010002 00000005 00000002 00000001 00000001 00000000 )
	Wait  25`
	ExecWait  *Var[C]
	Return
	End
}

/%
	ARGS:
	*Var[A]  entryID
	*Var[B]  colliderID
	*Var[C]  $Script_GotoMap
%/
#new:Script $Script_ExitHorizontalPipe
{
	Call  802D249C  ( *Var[0] )
	If  *Var[0]  ==  00000000
		Return
	EndIf
	Call  GetPlayerActionState  ( *Var[0] )
	If  *Var[0]  ==  .ActionState:Spin % 1A
		Return
	EndIf
	Call  GetCurrentPartner ( *Var[0] )
	If  *Var[0]  !=  .ActionState:Idle % 0
		Call  GetCurrentPartnerID   ( *Var[1] )
		If  *Var[1]  !=  .Partner:Watt % 6
			Return
		Else
			Call  802D2B6C ( )
			Call  DisablePlayerInput    ( .True )
		EndIf
	Else
		Call  DisablePlayerInput    ( .True )
	EndIf
	SetGroup  0000001B
	Call  $Function_Pipe_SetAnimFlag ( )
	Call  DisablePlayerPhysics  ( .True )
	Call  ModifyColliderFlags   ( 00000000 *Var[B] 7FFFFE00 )
	Set   *Var[0]  *Var[A]
	Call  $Function_Pipe_GetEntryCoords ( )
	Set   *Var[5]  *Var[1]
	Set   *Var[6]  *Var[2]
	Add   *Var[6]  2`
	Set   *Var[7]  *Var[3]
	Set   *Var[8]  *Var[4]
	Add   *Var[8]  180`
	If  *Var[4]  >=  360`
		Sub   *Var[4]  360`
	EndIf
	Call  InterpPlayerYaw       ( *Var[8] 00000001 )
	Wait  1`
	Call  PlaySound     ( 00000163 )
	Call  GetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
	Call  SetPlayerPos  ( *Var[0] *Var[6] *Var[7] )
	Call  SetPlayerAnimation    ( .PlayerAnim:StandStill )
	Call  802D286C      ( 00000800 )
	Call  802D2520      ( 00010000 00000005 00000003 00000001 00000001 00000000 )
	Thread
		Wait  8`
		Call  HidePlayerShadow  ( .True )
	EndThread
	Thread
		Wait  3`
		Loop  40`
			Call  $Function_Pipe_GetPointInFrontOfPlayer ( *Fixed[1.0] )
			Call  SetPlayerPos  ( *Var[0] *Var[1] *Var[2] )
			Wait  1`
		EndLoop
	EndThread
	Wait  25`
	ExecWait  *Var[C]
	Return
	End
}

%

#new:Function $Function_GetTattle
{
LIO       V0, $String_Tattle
JR        RA
RESERVED
}

#string $String_Tattle
{
[Style Tattle][EnableCDownNext]
Those pillars look pretty slippery![BR]
Watch your step!
[Wait][End]
}
